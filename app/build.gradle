apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'jacoco-android'

android {
  compileSdkVersion 26
  buildToolsVersion "26.0.2"
  defaultConfig {
    applicationId "com.android.szparag.todoist"
    minSdkVersion 16
    targetSdkVersion 26
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    multiDexEnabled true
  }
  buildTypes {
    noOptimization {
      minifyEnabled false
      shrinkResources false
      debuggable true
      jniDebuggable true
      renderscriptDebuggable true
      applicationIdSuffix '.debug_nooptimise'
      signingConfig signingConfigs.debug
    }
    debug {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      debuggable true
      jniDebuggable true
      renderscriptDebuggable true
      applicationIdSuffix '.debug'
    }
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      debuggable false
      jniDebuggable false
      renderscriptDebuggable false
    }
  }
  packagingOptions {
    exclude 'META-INF/**rxjava.properties**'
  }

//  todo: only in debug build
  lintOptions {
    abortOnError false
  }

  tasks.withType(Test) {
    testLogging {
      // set options for log level LIFECYCLE
      events "passed", "skipped", "failed", "standardOut"
      showExceptions true
      exceptionFormat "full"
      showCauses true
      showStackTraces true

      // set options for log level DEBUG and INFO
      debug {
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
      }
      info.events = debug.events
      info.exceptionFormat = debug.exceptionFormat


      afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
          def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
          def startItem = '|  ', endItem = '  |'
          def repeatLength = startItem.length() + output.length() + endItem.length()
          println('\n' + '\n' + '\n' + ('-' * repeatLength) + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength) + '\n' + '\n' + '\n')
        }
      }
    }
  }
}

kapt {
  generateStubs = true
}


dependencies {
  compile fileTree(include: ['*.jar'], dir: 'libs')

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.2.12'
  testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })


  debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
  noOptimizationCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
  releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

  //  support libs
  compile 'com.android.support:appcompat-v7:26.1.0'
  compile 'com.android.support:design:26.1.0'
  compile 'com.android.support:cardview-v7:26.1.0'
  compile 'com.android.support.constraint:constraint-layout:1.0.2'
  compile 'com.android.support:multidex:1.0.1'
  //  kotlin
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
  //  rx
  compile 'io.reactivex.rxjava2:rxjava:2.1.1'
  compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
  compile 'io.reactivex.rxjava2:rxkotlin:2.0.3'
  compile 'com.github.akarnokd:rxjava2-interop:0.10.2'
  //  rxbinding
  compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
  compile 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:2.0.0'
  //  dagger2
  compile 'com.google.dagger:dagger:2.9'
  kapt 'com.google.dagger:dagger-compiler:2.9'
  //  realm
  kapt 'io.realm:realm-annotations:3.5.0'
  kapt 'io.realm:realm-annotations-processor:3.5.0'
  // https://mvnrepository.com/artifact/joda-time/joda-time
  compile 'joda-time:joda-time:2.3'
  compile 'com.github.rubensousa:gravitysnaphelper:1.3'

}
